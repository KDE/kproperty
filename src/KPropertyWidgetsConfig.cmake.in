# KPropertyWidgetsConfig.cmake provides information about the installed KPropertyWidgets library.
# It can be used directly from CMake via find_package(KPropertyWidgets NO_MODULE)
#
# Any changes in this ".cmake" file will be overwritten by CMake, the source is the ".cmake.in" file.
#
# The following CMake variables are provided:
#   KPropertyWidgets_VERSION_MAJOR - the major version number of KPropertyWidgets
#   KPropertyWidgets_VERSION_MINOR - the minor version number of KPropertyWidgets
#   KPropertyWidgets_VERSION_PATCH - the patch version number of KPropertyWidgets
#   KPropertyWidgets_INCLUDE_DIRS  - the include directories to use
#
# Additionally, the following imported library targets are created, which may be used directly
# with target_link_libraries():
#   KPropertyWidgets - the KPropertyWidgets library

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
include(FeatureSummary)

find_dependency(Qt5Widgets @REQUIRED_QT_VERSION@)
find_dependency(KPropertyCore @PROJECT_VERSION@ EXACT)

include("${CMAKE_CURRENT_LIST_DIR}/KPropertyWidgetsTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/KPropertyWidgetsQCHTargets.cmake")

# Features
set(KPROPERTY_KF @KPROPERTY_KF@)

# Match COMPONENTS to features (KPROPERTY_ prefixes)
foreach (_COMP ${KPropertyWidgets_FIND_COMPONENTS})
    if(NOT DEFINED KPROPERTY_${_COMP} OR NOT ${KPROPERTY_${_COMP}})
        message(FATAL_ERROR "KPropertyWidgets does not contain required component ${_COMP}. Update KPropertyWidgets or remove this requirement.")
    endif()
endforeach()

set_package_properties(KPropertyWidgets PROPERTIES
   DESCRIPTION "A property editing framework with editor widget"
   URL "https://community.kde.org/KProperty")
